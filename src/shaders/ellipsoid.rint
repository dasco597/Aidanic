#version 460
#extension GL_NV_ray_tracing : require

#extension GL_GOOGLE_include_directive : require
#include "common.glsl"

layout(set = 1, binding = 1, std430) readonly buffer Ellipsoids { Ellipsoid ellipsoids[ELLIPSOID_COUNT_PER_TLAS]; };

hitAttributeNV HitPayload hit_payload;

float sdf_ellipsoid(vec3 point, vec3 center, vec3 radius)
{
	point -= center;
	float k0 = length(point / radius);
    float k1 = length(point / (radius*radius));
    return k0*(k0-1.0)/k1;
}

vec3 calc_normal(vec3 point, vec3 center, vec3 radius)
{
    vec2 e = vec2(0.0005,0.0);
    return normalize(vec3( 
        sdf_ellipsoid(point + e.xyy, center, radius) - sdf_ellipsoid(point - e.xyy, center, radius),
		sdf_ellipsoid(point + e.yxy, center, radius) - sdf_ellipsoid(point - e.yxy, center, radius),
		sdf_ellipsoid(point + e.yyx, center, radius) - sdf_ellipsoid(point - e.yyx, center, radius)));
}

void main()
{
	if (gl_InstanceID >= ELLIPSOID_COUNT_PER_TLAS) return;
	
    vec3 ray_o = gl_ObjectRayOriginNV;
    vec3 ray_d = normalize(gl_ObjectRayDirectionNV); // todo need to normalize?

    vec3 center = ellipsoids[gl_InstanceID].center.xyz;
    vec3 radius = ellipsoids[gl_InstanceID].radius.xyz;
	
	float depth = 0.0;
	for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
		vec3 point = ray_o + ray_d * depth;
		float dist = sdf_ellipsoid(point, center, radius);

		depth += dist;
		if (dist < EPSILON) {
			hit_payload.normal = vec4(gl_ObjectToWorldNV * vec4(calc_normal(point, center, radius), 1.0), 1.0);
			hit_payload.color = ellipsoids[gl_InstanceID].color;
			hit_payload.objectID = ellipsoids[gl_InstanceID].objectID;
			reportIntersectionNV(depth, uint(gl_InstanceID));
			return;
		}

		if (dist >= MAX_DISTANCE) {
			break;
		}
	}

	// to view the aabb
	//hit_payload.normal = vec4(0.0, 0.0, 1.0, 0.0);
	//hit_payload.color = vec4(1.0, 0.0, 0.0, 1.0);
	//reportIntersectionNV(100.0, 0u);
}